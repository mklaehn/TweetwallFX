/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2015-2022 TweetWallFX
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '6.3.0'
        classpath group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: '0.42.0'
        classpath group: 'com.netflix.nebula', name: 'gradle-info-plugin', version: '5.2.0'
        classpath group: 'com.netflix.nebula', name: 'nebula-publishing-plugin', version: '14.1.1'
        classpath group: 'gradle.plugin.com.github.spotbugs', name: 'spotbugs-gradle-plugin', version: '2.0.0'

        if (JavaVersion.current().isJava11Compatible()) {
            classpath group: 'net.ltgt.gradle', name: 'gradle-errorprone-plugin', version: '2.0.2'
        } else {
            throw new GradleException("Building with Java major version prior to 11 is not supported.")
        }

        // VCS dependency required by plugin 'gradle-info-plugin'
        classpath group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '6.+'
        classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '3.3'
        classpath group: 'com.google.gradle', name: 'osdetector-gradle-plugin', version: '1.7.0'
    }
}

apply plugin: 'base'
apply plugin: 'build-dashboard'
apply plugin: 'org.sonarqube'
apply plugin: 'com.google.osdetector'

// applying Versions plugin
apply plugin: 'com.github.ben-manes.versions'

ext {
    javaFxPlatform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
}

allprojects {
    group = 'org.tweetwallfx'
    version = currentVersion
    description = 'JavaFX based Tweetwall (' + name + ')'

    repositories {
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                // workaround for spotbugs not yet supporting JDK 13 class formats
                substitute module('org.ow2.asm:asm') with module ('org.ow2.asm:asm:9.2')
            }
        }
    }

    // configuring Spotless
    apply plugin: "com.diffplug.spotless"

    afterEvaluate {
        spotless {
//            if (!System.env.CI) {
//                // https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables
//                // only format files which have changed since this ref
//                // TODO: Figure out a way to get this working in CI (i.e. Github Workflows)
//                ratchetFrom 'origin/master'
//            }

            format 'textBasedFiles', {
                target(
                    '*.gradle',
                    '**/*.asciidoc',
                    '**/*.adoc',
                    '**/*.asc',
                    '**/*.css',
                    '**/*.json',
                    '**/*.md',
                    '**/*.properties',
                    '.gitignore',
                )

                trimTrailingWhitespace()
                indentWithSpaces(4)
                endWithNewline()
            }

            def closure = {
                trimTrailingWhitespace()
                indentWithSpaces(4)
                endWithNewline()
//                importOrder(
//                    '\\#',    // static imports
//                    'java',   // classes from java root package
//                    'javax',  // classes from javax root package
//                    '',       // any other import
//                )
                custom('noStarImports', {fileContent -> fileContent.replaceAll(/\nimport (static )?[a-zA-Z0-9.]*[.][\*];\n/, '\n')})
                custom('noMultipleEmptyLines', {fileContent -> fileContent.replaceAll('\n{3,}', '\n\n')})
                custom('noEmptyLineBeforeClosingBrace', {fileContent -> fileContent.replaceAll(/\n+(\n *[\}])/, '$1')})
                licenseHeaderFile rootProject.file('LICENSE.spotlessTemplate')
            }

            if (plugins.hasPlugin(org.gradle.api.plugins.GroovyPlugin)) {
                groovy(closure)
            } else if (plugins.hasPlugin(org.gradle.api.plugins.JavaPlugin)) {
                java(closure)
            }
        }
    }

    afterEvaluate {
        check.dependsOn(spotlessCheck)
    }

    // now configuring for projects that coontain code not just structuring the project
    if (childProjects.isEmpty()) {
        // configuring standard java project
        apply plugin: 'java-library'
        apply plugin: 'idea'
        apply plugin: 'jacoco'
        apply plugin: 'eclipse'
        apply plugin: 'nebula.info'
        apply plugin: 'maven-publish'

        configurations {
            javafx
            jaxb
        }

        dependencies {
            javafx group: 'org.openjfx', name: 'javafx-base', version: '17.0.2', classifier: javaFxPlatform
            javafx group: 'org.openjfx', name: 'javafx-graphics', version: '13.0.2', classifier: javaFxPlatform
            javafx group: 'org.openjfx', name: 'javafx-fxml', version: '13.0.2', classifier: javaFxPlatform
            javafx group: 'org.openjfx', name: 'javafx-controls', version: '13.0.2', classifier: javaFxPlatform

            jaxb group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'
            jaxb group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b+'
            jaxb group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.4.0-b+'

            testImplementation group: 'junit', name: 'junit', version: '4.13.2'
            testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.+'
        }

        jar {
            manifest.attributes provider: 'gradle'
        }

        javadoc {
            if (JavaVersion.current().isJava11Compatible()) {
                afterEvaluate {
                    def additionalModules = (configurations.javafx + configurations.jaxb)
                    options.addStringOption('-module-path', additionalModules.asPath)
                    options.addStringOption('-add-modules', 'java.activation,java.xml.bind,javafx.controls,javafx.graphics,javafx.fxml')
                }
            }
        }

        tasks.withType(JavaCompile) {
            options.compilerArgs.addAll([
                    '-Xlint:all',
                    '-Werror',
                    '-proc:none',
            ])

            if (JavaVersion.current().isJava11Compatible()) {
                afterEvaluate {
                    def additionalModules = (configurations.javafx + configurations.jaxb)
                    options.compilerArgs.addAll([
                            '--module-path',
                            additionalModules.asPath,
                            '--add-modules',
                            'java.activation,java.xml.bind,javafx.controls,javafx.graphics,javafx.fxml',
                    ])
                }
            }
        }

        tasks.withType(JavaExec) {
            if (JavaVersion.current().isJava11Compatible()) {
                afterEvaluate {
                    def additionalModules = (configurations.javafx + configurations.jaxb)
                    jvmArgs '--module-path'
                    jvmArgs additionalModules.asPath
                    jvmArgs '--add-modules'
                    jvmArgs 'java.activation,java.xml.bind,javafx.controls,javafx.graphics,javafx.fxml'
                }
            }
        }

        tasks.withType(Test) {
            if (JavaVersion.current().isJava11Compatible()) {
                afterEvaluate {
                    def additionalModules = (configurations.javafx + configurations.jaxb)
                    jvmArgs '--module-path'
                    jvmArgs additionalModules.asPath
                    jvmArgs '--add-modules'
                    jvmArgs 'java.activation,java.xml.bind,javafx.controls,javafx.graphics,javafx.fxml'
                }
            }
        }

        check {
            dependsOn 'javadoc'
        }

        // configuring Errorprone
        if (!JavaVersion.current().isJava12Compatible()) {
            apply plugin: 'net.ltgt.errorprone'

            dependencies {
                if (JavaVersion.current().isJava11Compatible()) {
                    errorprone group: 'com.google.errorprone', name: 'error_prone_core', version: '2.11.0'
                } else {
                    throw new GradleException("Building with Java major version prior to 11 is not supported.")
                }
            }
        }

        // configuring Spotbugs
        apply plugin: 'com.github.spotbugs'

        spotbugs {
            ignoreFailures = false
        }

        tasks.withType(com.github.spotbugs.SpotBugsTask) {
            // Workaround to bug https://github.com/spotbugs/spotbugs/issues/440
            def sourceSetName = (name - 'spotbugs').uncapitalize()
            dependsOn( sourceSets[sourceSetName].classesTaskName)

            reports {
                xml.enabled = false
                html.enabled = true
            }

            if (JavaVersion.current().isJava11Compatible()) {
                afterEvaluate {
                    classpath = (classpath + configurations.javafx)
                }
            }
        }

        // configuring Versions plugin
        dependencyUpdates {
            outputFormatter = 'plain,html'
            gradleReleaseChannel = 'current'

            resolutionStrategy {
                componentSelection { rules ->
                    rules.all { ComponentSelection selection ->
                        boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
                            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
                        }
                        if (rejected) {
                            selection.reject('Release candidate')
                        }
                    }
                }
            }
        }

        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.javadoc-jar'
        apply plugin: 'nebula.source-jar'
        apply plugin: 'nebula.publish-verification'
    }

    plugins.withType(org.gradle.api.plugins.GroovyPlugin) {
        dependencies {
            implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.10'
            testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.1-groovy-3.0'
        }
    }

    tasks.withType(org.gradle.api.tasks.testing.Test) {
        systemProperty 'org.tweetwallfx.tests.executeCFPClientLiveTests', executeCFPClientLiveTests
    }
}

task createFolders(description: 'Creates the folders for the projects and sourceSets should they not exist.') doLast {
    rootProject.allprojects.each {p ->
        if (!p.getProjectDir().isDirectory()) {
            println 'Creating project folder: ' + p.getProjectDir()
            p.getProjectDir().mkdirs()
        }

        if (p.hasProperty('sourceSets')) {
            p.sourceSets*.allSource*.srcDirs*.each { File srcDir ->
                if (!srcDir.isDirectory()) {
                    println 'Creating source folder: ' + srcDir
                    srcDir.mkdirs()
                }
            }
        }
    }
}
